<html>
  <body>
    <table width="80%" align="center">
      <tr>
        <td>
	  <p>
	    Problemet är att skriva ut indenterade strängar.  Det är
	    naturligt att man skapar en "global" (egentligen inte, men
	    det är överkurs) variabel som indikerar hur stor
	    indenteringen är för tillfället, och en funktion som
	    skriver ut strängar beroende på värdet i den variabeln:
	  </p>

	  <pre>
	    (defvar *indentation* 0)
	    
	    (defun write-indented-line (string)
	      (dotimes (i *indentation*)
		(write-string "  "))
	      (write-line string))
	  </pre>

<!--
	  <p>
	    För att abstrahera lite skriver vi två funktioner för att
	    öka och minska indenteringen:
	  </p>

	  <pre>
	    (defun increase-indentation ()
	      (incf *indentation*))
	    
	    (defun decrease-indentation ()
	      (decf *indentation*))
	  </pre>
-->

	  <p>
	    Ett exempel på hur man använder funktionerna kan se ut så
	    här:
	  </p>

<!--
	  <pre>
	    (defun write-some-c-code ()
	      (write-indented-line "void main (int argc, char **argv)")
	      (write-indented-line "{")
	      (incf *indentation*)
	      (write-indented-line "while (argc--)")
	      (incf *indentation*)
	      (write-indented-line "{")
	      (incf *indentation*)
	      (write-indented-line "printf (\"hello, world\");")
	      (decf *indentation*)
	      (write-indented-line "}")
	      (decf *indentation*)
	      (write-indented-line "exit (0);")
	      (decf *indentation*)
	      (write-indented-line "}"))
	  </pre>

	  <pre>
     	    void main (int argc, char **argv)
     	    {
  	      while (argc--)
  		{
  		  printf ("hello, world");
  		}
  	      exit (0);
     	    }
	  </pre>
-->

	  <pre>
	    (defun write-some-html ()
	      (write-indented-line "&lt;html&gt;")
	      (incf *indentation*)
	      (write-indented-line "&lt;body&gt;")
	      (incf *indentation*)
	      (write-indented-line "&lt;p&gt;")
	      (incf *indentation*)
	      (write-indented-line "hello, world")
	      (decf *indentation*)
	      (write-indented-line "&lt;/p&gt;")
	      (decf *indentation*)
	      (write-indented-line "&lt;/body&gt;")
	      (decf *indentation*)
	      (write-indented-line "&lt;/html&gt;"))
	  </pre>

	  <p>
	    Resultatet av att anropa den funktionen blir:
	  </p>

	  <pre>
	    &lt;html&gt;
	      &lt;body&gt;
		&lt;p&gt;
		  hello, world
		&lt;/p&gt;
	      &lt;/body&gt;
	    &lt;/html&gt;
	  </pre>

	  <p>
	    Ofta om man skriver i C eller liknande stannar man (i alla
	    fall jag, får jag erkänna) vid den här nivån.  Men i Lisp
	    känns det mer naturligt att snygga till koden ytterligare.
	  </p>

	  <p>
	    Först ville jag skapa ett makro som gör att en ökning av
	    indenteringen alltid paras ihop med en motsvarande
	    minskining av indenteringen:
	  </p>

	  <pre>
	    (defmacro with-indentation (&rest body)
	      `(let ((*indentation* (1+ *indentation*)))
		 ,@body))
	  </pre>

	  <p>
	    Ovanstående funktion kan då skrivas om som:
	  </p>

	  <pre>
	    (defun write-some-html ()
	      (write-indented-line "&lt;html&gt;")
	      (with-indentation
		(write-indented-line "&lt;body&gt;")
		(with-indentation
		  (write-indented-line "&lt;p&gt;")
		  (with-indentation
		    (write-indented-line "hello, world"))
		  (write-indented-line "&lt;/p&gt;"))
		(write-indented-line "&lt;/body&gt;"))
	      (write-indented-line "&lt;/html&gt;"))
	  </pre>

	  <p>
	    Det ser bättre ut, va?  Strukturen i output reflekteras
	    direkt i kodstrukturen.
	  </p>

	  <p>
	    I HTML har en tag alltid en motsvarande sluttag.  Det kan
	    man ju också skriva ett makro för:
	  </p>

	  <pre>
	    (defun strcat (&rest strings)
	      (apply #'concatenate 'string strings))
      
	    (defmacro with-enclosing-tag (name &rest body)
	      `(progn
		 (write-indented-line (strcat "&lt;" ,name "&gt;"))
		 (with-indentation
		   ,@body)
		 (write-indented-line (strcat "&lt;/" ,name "&gt;"))))
	  </pre>

	  <p>
	    Nu kan man skriva om funktionen så här:
	  </p>

	  <pre>
	    (defun write-some-html ()
	      (with-enclosing-tag "html"
		(with-enclosing-tag "body"
		  (with-enclosing-tag "p"
		    (write-indented-line "hello, world")))))
	  </pre>

<!--
	  <pre>
	    (defmacro deftag (name)
	      `(defmacro ,name (&body body)
		 `(with-enclosing-tag ,',(symbol-name name)
		    ,@body)))
      
	    (deftag html)
	    (deftag body)
	    (deftag p)
	  </pre>

	  <pre>
	    (defun write-some-html ()
	      (html
		(body
		  (p
		    (write-indented-line "hello, world")))))
	  </pre>
-->

	  <p>
	    Ta da!  Inte mycket kod kvar.  Bara precis det som stämmer
	    överens med den mentala modellen av vad som ska göras
	    (åtminståne min modell).
	  </p>

	  <p>
	    PS.  Ovanstående Common Lisp-kod funkar även i Emacs om
	    man först gör:
	  </p>

	  <pre>
	    (require 'cl)
	    (defun write-string (string)
	      (insert string))
	    (defun write-line (string)
	      (insert string)
	      (insert ?\n))
	  </pre>
	</td>
      </td>
    </table>
  </body>
</html>
